@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# ViennaLS requires C++17
set(CMAKE_CXX_STANDARD "17")

# ##################################################################################################
# compiler dependent settings for ViennaLS
# ##################################################################################################
if(OMP_PARALLELIZE)
  find_dependency(OpenMP)
  list(APPEND VIENNALS_LIBRARIES OpenMP::OpenMP_CXX)
endif()

# compiler dependent settings
if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
  # disable-new-dtags sets RPATH which searches for libs recursively, instead of RUNPATH which is
  # not needed for g++ to link correctly
  set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -Wl,--disable-new-dtags")
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
  # SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /openmp /wd\"4267\" /wd\"4244\"")
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /wd\"4267\" /wd\"4244\"")
endif()

set(VIENNALS_INCLUDE_DIRS "@CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_INCLUDEDIR@")

set(ViennaHRLE_DIR @ViennaHRLE_DIR@)

find_dependency(ViennaHRLE PATHS ${ViennaHRLE_DIR} NO_DEFAULT_PATH)
list(APPEND VIENNALS_INCLUDE_DIRS ${VIENNAHRLE_INCLUDE_DIRS})

set(VIENNALS_USE_VTK @VIENNALS_USE_VTK@)
if(@VIENNALS_USE_VTK@)
  set(VTK_DIR @VTK_DIR@)

  # If VIENNALS_USE_VTK is set to true, we expect vtk to be found. Otherwise throw an error.
  # NO_DEFAULT_PATH ensures that the path is stored in cmake cache and consecutive find_dependency
  # calls don't overwrite its value.
  find_dependency(
    VTK
    CONFIG
    PATHS
    ${VTK_DIR}
    COMPONENTS
    @VTK_COMPONENTS@
    NO_DEFAULT_PATH)

  add_compile_definitions(VIENNALS_USE_VTK)

  # use VTK_USE_FILE for older versions of VTK
  if(VTK_VERSION VERSION_LESS "9.0")
    message(STATUS "Using old VTK Version: ${VTK_VERSION}")
    include(${VTK_USE_FILE})
  endif()

  set(VTK_LIBRARIES @VTK_LIBRARIES@)

  list(APPEND VIENNALS_LIBRARIES ${VTK_LIBRARIES})
endif()

if(@VIENNALS_PRECOMPILE_HEADERS@)
  option(VIENNALS_USE_PRECOMPILED "Use precompiled headers" ON)
  if(VIENNALS_USE_PRECOMPILED)
    add_definitions(-DVIENNALS_USE_PRECOMPILED)
    link_directories(@CMAKE_INSTALL_PREFIX@/lib)
    list(APPEND VIENNALS_LIBRARIES @PROJECT_NAME@)
  endif()
endif()

if(@VIENNALS_STATIC_BUILD@)
  set(VIENNALS_STATIC_BUILD ON)
endif()

check_required_components("@PROJECT_NAME@")
